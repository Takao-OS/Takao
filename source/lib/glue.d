/// Implementation of functions called by the generated code and not directly.
/// PLEASE DO NOT CALL ANY OF THIS EXPLICITLY.
/// Thanks.
module lib.glue;

import lib.string: fromCString;
import lib.panic:  panic;

// Deprecate everything so we dont call it from code.
deprecated {
    /// Called when doing `assert()`.
    extern (C) void __assert(const char* exp, const char* file, uint line) {
        panic("Assertion failed in '", fromCString(file), "' line ", line);
    }

    /// Generated by the compiler as optimization.
    extern (C) void *memcpy(void* dest, const void* src, size_t n) {
        auto pdest = cast(ubyte*)dest;
        auto psrc  = cast(const ubyte*)src;

        for (size_t i = 0; i < n; i++) {
            pdest[i] = psrc[i];
        }

        return dest;
    }

    /// Generated by the compiler as optimization.
    extern (C) void* memset(void* s, int c, ulong n) {
        auto pointer = cast(ubyte*)s;

        foreach (i; 0..n) {
            pointer[i] = cast(ubyte)c;
        }

        return s;
    }

    /// Generated by the compiler as optimization.
    extern (C) int memcmp(const void *s1, const void *s2, size_t n) {
        auto p1 = cast(ubyte*)s1;
        auto p2 = cast(ubyte*)s2;

        for (size_t i = 0; i < n; i++) {
            if (p1[i] != p2[i]) {
                return p1[i] < p2[i] ? -1 : 1;
            }
        }

        return 0;
    }
}
